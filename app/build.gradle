import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "jp.juggler.subwaytooter"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 199
        versionName "1.9.9"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // https://stackoverflow.com/questions/47791227/java-lang-illegalstateexception-dex-archives-setting-dex-extension-only-for
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Specifies comma-separated list of flavor dimensions.
    flavorDimensions "rcOrDev"

    productFlavors {
        rc {
            dimension "rcOrDev"
        }
    }

    dexOptions {
        jumboMode = true
    }

    // Generate Signed APK のファイル名を変更
    android.applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all {
                // Rename APK
                def versionCode = defaultConfig.versionCode
                def versionName = defaultConfig.versionName
                def flavor = variant.flavorName
                def date = new SimpleDateFormat("yyyyMMdd_HHmmss").format(new Date())
                outputFileName = "../../SubwayTooter-${flavor}-${versionCode}-${versionName}-${date}.apk"
            }
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile project(':exif')
    compile project(':colorpicker')
    compile project(':emoji')

    compile 'com.android.support:support-v4:27.0.2'
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.android.support:customtabs:27.0.2'

    compile 'com.google.firebase:firebase-core:11.8.0'
    compile 'com.google.firebase:firebase-messaging:11.8.0'

    // testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    compile 'uk.co.chrisjenx:calligraphy:2.3.0'
    compile 'com.github.woxthebox:draglistview:1.5.1'
    compile 'com.github.omadahealth:swipy:1.2.3@aar'
    // compile 'com.jrummyapps:colorpicker:2.1.7'
    compile 'com.github.kenglxn.QRGen:android:2.3.0'

    compile 'com.squareup.okhttp3:okhttp:3.9.1'

    compile 'commons-io:commons-io:2.6'

    compile 'com.github.bumptech.glide:glide:4.5.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:4.5.0'
    compile 'com.github.bumptech.glide:annotations:4.5.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.5.0'

    compile 'org.hjson:hjson:3.0.0'

    compile 'com.google.android:flexbox:0.3.1'

    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"


    implementation 'com.google.android.exoplayer:exoplayer:r2.5.4'
}

repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
