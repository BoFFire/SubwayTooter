import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "jp.juggler.subwaytooter"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 192
        versionName "1.9.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Specifies comma-separated list of flavor dimensions.
    flavorDimensions "rcOrDev"

    productFlavors {
        rc {
            dimension "rcOrDev"
        }
    }

    dexOptions {
        jumboMode = true
    }

    // Generate Signed APK のファイル名を変更
    android.applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all {
                // Rename APK
                def versionCode = defaultConfig.versionCode
                def versionName = defaultConfig.versionName
                def flavor = variant.flavorName
                def date = new SimpleDateFormat("yyyyMMdd_HHmmss").format(new Date())
                outputFileName = "../../SubwayTooter-${flavor}-${versionCode}-${versionName}-${date}.apk"
            }
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile project(':exif')
    compile project(':colorpicker')

    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:customtabs:26.1.0'

    compile 'com.google.firebase:firebase-core:11.6.2'
    compile 'com.google.firebase:firebase-messaging:11.6.2'

    testCompile 'junit:junit:4.12'
    compile 'uk.co.chrisjenx:calligraphy:2.3.0'
    compile 'com.github.woxthebox:draglistview:1.5.1'
    compile 'com.github.omadahealth:swipy:1.2.3@aar'
    // compile 'com.jrummyapps:colorpicker:2.1.7'
    compile 'com.github.kenglxn.QRGen:android:2.3.0'

    compile 'com.squareup.okhttp3:okhttp:3.9.1'

    // commons-io は 2.6にすると closeQuietly のかわりに try-with-resourceを使えと煩い
    compile 'commons-io:commons-io:2.4'

    // com.github.bumptech.glide 4.x はサポートライブラリ27に依存してる
    // SDKのソースの27が提供されたら上げる
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.5.0'

    compile 'org.hjson:hjson:3.0.0'

    compile 'com.google.android:flexbox:0.3.1'

    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"


    implementation 'com.google.android.exoplayer:exoplayer:r2.5.4'
}

repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
